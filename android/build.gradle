buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 23
        compileSdkVersion = 34
        targetSdkVersion = 34
        kotlinVersion = "1.9.22"
        gradleVersion = "8.8.0"
        firebaseBomVersion = "32.7.0"
        ndkVersion = "25.2.9519653"
        reactNativeVersion = "0.75.0"
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://maven.pkg.github.com/facebook/react-native")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: ""
                password = System.getenv("GITHUB_TOKEN") ?: ""
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'org.bouncycastle' &&
                    details.requested.name == 'bcprov-jdk18on') {
                    // Previeni il download della versione corrotta
                    details.useVersion '1.79'
                    details.because("Use local cached version to avoid corrupted download")
                }
            }
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:$gradleVersion")
        classpath("com.google.gms:google-services:4.4.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("com.google.firebase:firebase-crashlytics-gradle:2.9.9")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven { url 'https://www.jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                ndkVersion rootProject.ext.ndkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion

                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }

                packagingOptions {
                    exclude "**/bcprov-jdk*.jar"
                }
            }
        }
    }
}

