plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.facebook.react'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
}

def hermesEnabled = project.hasProperty("hermesEnabled")? project.hermesEnabled.toBoolean(): true

def jscFlavor = "org.webkit:android-jsc:+"

android {
    namespace "fit.app.mobile"
    compileSdk 34
    ndkVersion rootProject.ext.ndkVersion

    defaultConfig {
        applicationId "fit.app.mobile"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("my-release-key.keystore")
            storePassword "Cl1mb!ng.Ks"
            keyAlias "my-key-alias"
            keyPassword "Cl1mb!ng.Ks"
        }

        release {
            if (System.getenv("FITAPP_STORE_PASSWORD") != null) {
                storeFile file("my-release-key.keystore")
                storePassword System.getenv("FITAPP_STORE_PASSWORD")
                keyAlias System.getenv("FITAPP_KEY_ALIAS")
                keyPassword System.getenv("FITAPP_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig true
    }

    sourceSets {
        main {
            java.srcDirs += [
                "src/main/java",
                "build/generated/rn_codegen/src/main/java",
                "build/generated/rncli/src/main/java",
                "build/generated/source/codegen/java"
            ]
        }
        debug {
            java.srcDirs += [
                "build/generated/rncli/src/main/java",
                "build/generated/rn_codegen/src/main/java",
                "build/generated/source/codegen/java"
            ]
        }
        release {
            java.srcDirs += [
                "build/generated/rn_codegen/src/main/java",
                "build/generated/source/codegen/java"
            ]
        }
    }
}

react {
    root = file("../../")
    entryFile = file("../../index.js")
    hermesCommand = file(
            "../../node_modules/react-native/sdks/hermesc/win64-bin/hermesc.exe"
    ).absolutePath
}

dependencies {
    implementation("com.facebook.react:react-android")

    if (hermesEnabled) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation(jscFlavor)
    }

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:${rootProject.ext.firebaseBomVersion}")
    implementation "com.google.firebase:firebase-auth-ktx"
    implementation "com.google.firebase:firebase-firestore-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-functions-ktx"
    implementation "com.google.firebase:firebase-storage-ktx"

    // Android base
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.multidex:multidex:2.0.1"

    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'



}

afterEvaluate {
    tasks.matching { it.name == "compileDebugKotlin" || it.name == "compileReleaseKotlin" }.all {
        dependsOn tasks.matching { it.name.startsWith("generateAutolinking") }
    }
}

gradle.projectsEvaluated {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all { kotlinCompile ->
        kotlinCompile.dependsOn(":app:generateCodegenArtifactsFromSchema")
        kotlinCompile.dependsOn(":app:generateCodegenSchemaFromJavaScript")
    }
}

