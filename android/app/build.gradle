apply plugin: "com.android.application"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "org.jetbrains.kotlin.android"

project.ext.react = [
    enableHermes: true,
    bundleInDebug: false,
    devDisabledInRelease: true,
    hermesCommand: "../../node_modules/hermes-engine/%OS-BIN%/hermesc",
    bundleCommand: "ram-bundle",
    extraPackagerArgs: ["--indexed-ram-bundle"]
]

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    ndkVersion rootProject.ext.ndkVersion
    namespace "fit.app.mobile"

    defaultConfig {
        applicationId "fit.app.mobile"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        resValue "string", "build_config_package", "fit.app.mobile"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        ndkVersion rootProject.ext.ndkVersion
    }

    signingConfigs {
        debug {
            storeFile file("my-release-key.keystore")
            storePassword "Cl1mb!ng.Ks"
            keyAlias "my-key-alias"
            keyPassword "Cl1mb!ng.KS"
        }
        release {
            storeFile file('fitapp-release-key.keystore')
            storePassword System.getenv("FITAPP_STORE_PASSWORD") ?: ""
            keyAlias System.getenv("FITAPP_KEY_ALIAS") ?: ""
            keyPassword System.getenv("FITAPP_KEY_PASSWORD") ?: ""
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            ext.enableCrashlytics = false
            resValue "string", "CodePushDeploymentKey", '""'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            zipAlignEnabled true
            debuggable false
            resValue "string", "CodePushDeploymentKey", '""'

            firebaseCrashlytics {
                //nativeSymbolUploadEnabled true
                strippedNativeLibsDir "build/intermediates/stripped_native_libs/release/out/lib"
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/release/out/lib"
            }
        }
    }

    packagingOptions {
        // Esclusioni complete per BouncyCastle e conflitti
        exclude "**/bcprov-jdk*.jar"
        exclude "**/libjsc.so"
        exclude "**/libfbjni.so"
        pickFirst "**/libc++_shared.so"
        exclude "META-INF/**"
        exclude "**/org/bouncycastle/**"
    }

    aaptOptions {
        ignoreAssetsPattern "!*.zip:!*.so:!*/armeabi-v7a/*:!*/arm64-v8a/*"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    // React Native core (con esclusioni esplicite)
    implementation("com.facebook.react:react-android:${rootProject.ext.reactNativeVersion}") {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
    }
    implementation("com.facebook.react:hermes-android:${rootProject.ext.hermesEngineVersion}") {
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk18on'
    }



    // Firebase (senza dipendenze crittografiche ridondanti)
    implementation platform("com.google.firebase:firebase-bom:${rootProject.ext.firebaseBomVersion}")
    implementation "com.google.firebase:firebase-firestore"
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-crashlytics-ndk"
    implementation "com.google.firebase:firebase-functions"

    // Support libraries
    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.multidex:multidex:2.0.1"

    // React Native libraries
    implementation project(":react-native-vector-icons")
    implementation project(":react-native-sqlite-storage")
}

apply from: file("../../node_modules/react-native-vector-icons/fonts.gradle")
apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle")
applyNativeModulesAppBuildGradle(project)
